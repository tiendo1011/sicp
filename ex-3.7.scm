(define (make-account balance password)
  (define (withdraw amount)
    (if (>= balance amount)
        (begin (set! balance (- balance amount))
               balance)
        "Insufficient funds"))
  (define (deposit amount)
    (set! balance (+ balance amount))
    balance)
  (define (dispatch provided-password m)
    (cond ((eq? m 'valid-password?) (eq? provided-password password))
          ((not (eq? provided-password password)) (error "Incorrect password"))
          ((eq? m 'withdraw) withdraw)
          ((eq? m 'deposit) deposit)
          (else (error "Unknown request: MAKE-ACCOUNT" m))))
  dispatch)

(define (make-joint original-acc original-acc-password joint-acc-password)
  (if (original-acc original-acc-password 'valid-password?)
      (lambda (provided-joint-acc-password action)
              (if (eq? provided-joint-acc-password joint-acc-password)
                  (original-acc original-acc-password action)
                  (error "Incorrect password")))
      (error "Wrong password for original account")))

(define peter-acc (make-account 100 'open-sesame))
(define paul-acc
  (make-joint peter-acc 'open-sesame 'rosebud))

((paul-acc 'rosebud 'withdraw) 40)
((paul-acc 'rosebud 'withdraw) 40)
